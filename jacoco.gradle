jacoco {
    toolVersion = "0.8.7"
    reportsDirectory = file("${buildDir}/reports/coverage")
}

project.afterEvaluate { project ->
    setupReporting()
}

def setupReporting() {
    tasks.withType(Test) {
        // Whether or not classes without source location should be instrumented
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type ->
        type.name
    }
    def productFlavors = android.productFlavors.collect { flavor ->
        flavor.name
    }
    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')
    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName
            if (!productFlavorName) {
                sourceName = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
            }
            def coverageName = "${sourceName}UnitTest"
            def testTaskName = "test${sourceName.capitalize()}UnitTest"
            def testTaskNameReport = "create${sourceName.capitalize()}CoverageReport"

            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
            task "jacoco${sourceName.capitalize()}CombinedTestReport"(type: JacocoReport, dependsOn: ["$testTaskName", "$testTaskNameReport"]) {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${testTaskName}, ${testTaskNameReport}, $coverageName"

                reports {
                    csv.enabled true // change if needed
                    xml.enabled true // change if needed
                    html.enabled true
                }

                def fileFilter = [
                        // data binding
                        'android/databinding/**/*.class',
                        '**/android/databinding/*Binding.class',
                        '**/android/databinding/*',
                        '**/androidx/databinding/*',
                        '**/BR.*',
                        // android
                        '**/R.class',
                        '**/R$*.class',
                        '**/BuildConfig.*',
                        '**/Manifest*.*',
                        '**/*Test*.*',
                        'android/**/*.*',
                        // kotlin
                        '**/*MapperImpl*.*',
                        '**/*$ViewInjector*.*',
                        '**/*$ViewBinder*.*',
                        '**/BuildConfig.*',
                        '**/*Component*.*',
                        '**/*BR*.*',
                        '**/Manifest*.*',
                        '**/*$Lambda$*.*',
                        '**/*Companion*.*',
                        '**/*Module*.*',
                        '**/*Dagger*.*',
                        '**/*Hilt*.*',
                        '**/*MembersInjector*.*',
                        '**/*_MembersInjector.class',
                        '**/*_Factory*.*',
                        '**/*_Provide*Factory*.*',
                        '**/*Extensions*.*',
                        // sealed and data classes
                        '**/*$Result.*',
                        '**/*$Result$*.*',
                        // adapters generated by moshi
                        '**/*JsonAdapter.*',
                ]

                def javaTree = fileTree(dir: "${project.buildDir}/intermediates/javac/$sourceName/classes", excludes: fileFilter)
                def kotlinTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/$sourceName", excludes: fileFilter)
                classDirectories.from = files([javaTree], [kotlinTree])

                def coverageSourceDirs = [android.sourceSets.main.java.srcDirs, "src/main/kotlin", "src/$productFlavorName/java", "src/$buildTypeName/java"]

                sourceDirectories.setFrom(files(coverageSourceDirs))

                //executionData.from = files("${project.buildDir}/outputs/unit_test_code_coverage/${testTaskName}/${testTaskName}.exec")
                executionData.from = fileTree(dir: "${project.buildDir}", includes: ["outputs/unit_test_code_coverage/$coverageName/${testTaskName}.exec"])

            }
        }
    }
}
